grammar fr.inria.diverse.melange.Melange with org.eclipse.xtext.xbase.Xbase

import "http://www.inria.fr/diverse/melange"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
//import "http://kompren/fr.inria.diverse.kompren" as kompren

ModelTypingSpace:
	('package' name = QualifiedName)?
	(imports = XImportSection)?
	(elements += Element)*
;

Element:
	Metamodel | EcoreModelType | TransformationDecl | Mapping
;

TransformationDecl returns Transformation:
	XbaseTransformation// | KomprenSlicer
;

Metamodel:
	'metamodel' name = ValidID (inheritanceRelation = Inheritance)?
	('implements' implements += [ModelType | QualifiedName] (',' implements += [ModelType | QualifiedName])*)?
	('requires' requires += [ModelType | QualifiedName] (',' requires += [ModelType | QualifiedName])*)?
	'{'
		('resource' resourceType = ResourceType ('uri' resourceUri = STRING | 'setup' xtextSetupRef = JvmTypeReference)?)? &
		('ecore' ecoreUri = STRING ('withGenmodel' (genmodelUris += STRING (',' genmodelUris += STRING)*))?)? &
		('exactType' exactTypeName = ValidID) &
		(aspects += Aspect)*
	'}'
;

enum ResourceType:
	MELANGE = 'Melange' | EMF = 'EMF' | XTEXT = 'Xtext'
;

Inheritance:
	'inherits' superMetamodel = [Metamodel | QualifiedName]
;

EcoreModelType returns ModelType:
	'modeltype' name = ValidID '{'
		'ecore' ecoreUri = STRING
	'}'
;

XbaseTransformation:
	(main ?= '@Main')?
	'transformation' (returnTypeRef = JvmTypeReference)? name = ValidID
	'(' (parameters += FullJvmFormalParameter (',' parameters += FullJvmFormalParameter)*)? ')'
	body = XBlockExpression
;

//KomprenSlicer:
//	slicer = Slicer
//;

Aspect:
	'with' aspectTypeRef = JvmTypeReference
;

Mapping returns Mapping:
	"map" from=STRING "to" to=STRING "{"
		(rules+=ClassMapping)*
	"}" 
;

ClassMapping returns ClassBinding:
	from=STRING "to" to=STRING "{"
		(properties+=PropertyMapping)*
	"}"
;

PropertyMapping returns PropertyBinding:
	from=STRING ":" to=STRING
;

/* --- Kompren Rules --- */
//Slicer returns kompren::Slicer: 'slicer' (active?='active')? (strict?='strict')? name=QualifiedName '{'
//		(('domain' ':' uriMetamodel+=STRING)+ &
//		('outputDomain' ':' outputMetamodel=STRING)? &
//		('input' ':' inputClasses+=[ecore::EClass|QualifiedName] ( "," inputClasses+=[ecore::EClass|QualifiedName])*) &
//		('radius' ':' radius=Radius)? &
//		(('slicedClass' ':' slicedElements+=SlicedClass) |
//		('slicedProperty' ':' slicedElements+=SlicedProperty))* &
//		('onStart' onStart=CODE_BLOCK)? &
//		('onEnd' onEnd=CODE_BLOCK)? &
//		('helper' helper=CODE_BLOCK)?)
//	'}';
//
//Constraint returns kompren::Constraint: name=ValidID expression=CODE_BLOCK;
//
//Radius returns kompren::Radius: {kompren::Radius} (focusedClasses+=[kompren::SlicedClass|QualifiedName] ( "," focusedClasses+=[kompren::SlicedClass|QualifiedName])* )?;
//
//SlicedClass returns kompren::SlicedClass: domain=[ecore::EClass|QualifiedName] (isOption?='option')? (ctx=VarDecl)? 
//	(expression=CODE_BLOCK)? ('constraint' ':' constraints+=Constraint)*;
//
//VarDecl returns kompren::VarDecl: varName=ValidID;
//
//SlicedProperty returns kompren::SlicedProperty:
//	domain=[ecore::EStructuralFeature|QualifiedName] (isOption?='option')? (opposite=OppositeCreation)? (src=VarDecl tgt=VarDecl)?
//		 (expression=CODE_BLOCK)? ('constraint' ':' constraints+=Constraint)*;
//
//OppositeCreation returns kompren::OppositeCreation: {kompren::OppositeCreation} 'opposite' '(' name=ValidID ')';
//
//terminal CODE_BLOCK : '[[' -> ']]';
