grammar fr.inria.diverse.k3.sle.K3SLE with org.eclipse.xtext.xbase.Xbase

import "http://www.inria.fr/diverse/k3/sle"

ModelTypingSpace:
	('package' name = QualifiedName)?
	(imports = XImportSection)?
	(elements += Element)*
;

Element:
	Metamodel | EcoreModelType | Transformation
;

Metamodel:
	'metamodel' name = ValidID (inheritanceRelation = Inheritance)?
	('implements' implements += [ModelType | QualifiedName] (',' implements += [ModelType | QualifiedName])*)?
	'{'
		('resource' resourceType = ResourceType ('uri' resourceUri = STRING | 'setup' resourceSetup = JvmTypeReference)?)?
		(ecore = EcoreImport)?
		('exactType' exactTypeRef = ValidID)
		(aspects += AspectImport)*
	'}'
;

enum ResourceType:
	K3 = 'K3' | EMF = 'EMF' | XTEXT = 'Xtext'
;

Inheritance:
	'inherits' superMetamodel = [Metamodel | QualifiedName]
;

EcoreModelType returns ModelType:
	'modeltype' name = ValidID '{'
		(ecore = EcoreImport)?
	'}'
;

Transformation:
	(main ?= '@Main')?
	'transformation' (returnTypeRef = JvmTypeReference)? name = ValidID
	'(' (parameters += FullJvmFormalParameter (',' parameters += FullJvmFormalParameter)*)? ')'
	body = XBlockExpression
;

EcoreImport:
	'ecore' uri = STRING ('withGenmodel' (genmodelUris += STRING (',' genmodelUris += STRING)*))?
;

AspectImport:
	'aspect' aspectRef = JvmTypeReference
;
